plugins {
    alias(libs.plugins.gradle.plugin.publish)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(gradleApi())

    implementation(libs.common.lang3)
    implementation(libs.bundles.asm)
}

base {
    archivesName = "${project.property("plg.archives_base_name")}"
    group = project.property("plg.maven_group")
    version = project.property("plg.version")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    website = "https://github.com/Hendrix-Shen/replace-token"
    vcsUrl = "https://github.com/Hendrix-Shen/replace-token"

    plugins {
        register("replacetoken") {
            id = "top.hendrixshen.replace-token"
            displayName = "ReplaceToken"
            description = "A gradle to replace token in class file."
            implementationClass = "top.hendrixshen.replacetoken.ReplaceTokenPlugin"
            tags = ["Minecraft", "Mod"]
        }
    }
}

tasks.withType(Javadoc).configureEach { Javadoc task ->
    include("**/api/**")

    task.options.encoding = "UTF-8"
    (task.options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
}

tasks.withType(JavaCompile).configureEach { JavaCompile task ->
    task.options.encoding = "UTF-8"
}
